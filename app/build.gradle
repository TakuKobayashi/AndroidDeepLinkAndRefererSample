import com.github.triplet.gradle.androidpublisher.ReleaseStatus

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'com.github.triplet.play'

private def loadLocalProperties() {
    Properties properties = new Properties()
    def localPropertiesFile = project.rootProject.file('local.properties');
    if(!localPropertiesFile.exists()){
        localPropertiesFile.createNewFile();
    }
    properties.load(localPropertiesFile.newDataInputStream())
    return properties
}

def properties = loadLocalProperties();

def applicationVersionName = "1.5.0"
def versionNameList = applicationVersionName.split(/\./)
def powCounter = versionNameList.size()
def versionNumbers = versionNameList.collect{numberString -> numberString.toInteger()}
def applicationVersionCode = versionNumbers.sum{ number ->
    powCounter = powCounter - 1
    (Math.pow(100, powCounter) * number).toInteger()
}

android {
    namespace 'com.taku.kobayashi.refarorsample'
    compileSdk = 36
    defaultConfig {
        applicationId "com.taku.kobayashi.refarorsample"
        minSdkVersion 21
        targetSdkVersion 36
        versionCode applicationVersionCode
        versionName applicationVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    signingConfigs {
        release {
            if(!properties.getProperty("RELEASE_STORE_FILE", "").empty){
                storeFile file(properties.getProperty("RELEASE_STORE_FILE", ""))
            }
            storePassword properties.getProperty("RELEASE_STORE_PASSWORD", "")
            keyAlias properties.getProperty("RELEASE_KEY_ALIAS", "")
            keyPassword properties.getProperty("RELEASE_KEY_PASSWORD", "")
        }
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

if(!properties.getProperty("GOOGLE_PLAY_SERVICE_ACCOUNT_CREDENTIALS_FILE_PATH", "").empty) {
    apply plugin: 'com.github.triplet.play'

    play {
        serviceAccountCredentials.set(file(properties.getProperty("GOOGLE_PLAY_SERVICE_ACCOUNT_CREDENTIALS_FILE_PATH", "")))
        track.set("internal")
        artifactDir.set(file("build/outputs/bundle/release/"))
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:2.1.20"
    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'com.android.installreferrer:installreferrer:2.2'
    implementation 'com.google.android.play:asset-delivery:2.3.0'
    implementation 'com.google.android.play:review:2.0.2'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
}

play {
    def androidDefaultConfig = android.getDefaultConfig()
    if(!properties.getProperty("GOOGLE_PLAY_PUBLISH_API_CONFIG_FILE", "").empty) {
        serviceAccountCredentials = file(properties.getProperty("GOOGLE_PLAY_PUBLISH_API_CONFIG_FILE", ""))
    }
    artifactDir = file("build/outputs/bundle/release/")
    // Google Playにreleaseする段階名 internal/alpha/beta/production のどれか(defaultは internal)
    track = "beta"
    // リリースのStatusに含まれているReleaseStatusの COMPLETED/DRAFT/HALTED/IN_PROGRESS のどれか(defaultは COMPLETED)
    // IN_PROGRESSだと部分公開という意味
    releaseStatus = ReleaseStatus.DRAFT
    releaseName = "${androidDefaultConfig.versionCode} (${androidDefaultConfig.versionName})"
}