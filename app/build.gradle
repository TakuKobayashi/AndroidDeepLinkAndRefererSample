apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'

def properties = loadLocalProperties();

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"
    defaultConfig {
        applicationId "com.taku.kobayashi.refarorsample"
        minSdkVersion 14
        targetSdkVersion 30
        versionCode 10402
        versionName "1.4.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            if(!properties.getProperty("RELEASE_STORE_FILE", "").empty){
                storeFile file(properties.getProperty("RELEASE_STORE_FILE", ""))
            }
            storePassword properties.getProperty("RELEASE_STORE_PASSWORD", "")
            keyAlias properties.getProperty("RELEASE_KEY_ALIAS", "")
            keyPassword properties.getProperty("RELEASE_KEY_PASSWORD", "")
        }
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

if(!properties.getProperty("GOOGLE_PLAY_SERVICE_ACCOUNT_CREDENTIALS_FILE_PATH", "").empty) {
    apply plugin: 'com.github.triplet.play'

    play {
        serviceAccountCredentials.set(file(properties.getProperty("GOOGLE_PLAY_SERVICE_ACCOUNT_CREDENTIALS_FILE_PATH", "")))
        track.set("internal")
        artifactDir.set(file("build/outputs/bundle/release/"))
    }
}

private def loadLocalProperties() {
    Properties properties = new Properties()
    def localPropertiesFile = project.rootProject.file('local.properties');
    if(!localPropertiesFile.exists()){
        localPropertiesFile.createNewFile();
    }
    properties.load(localPropertiesFile.newDataInputStream())
    return properties
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.android.play:core:1.10.0'
    implementation 'com.android.installreferrer:installreferrer:2.2'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
